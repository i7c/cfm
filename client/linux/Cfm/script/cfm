#!/usr/bin/env perl
use warnings FATAL => 'all';
use strict;
use Cfm::Bean;
use Cfm::Common::Logging;
use Cfm::Ui::Cli;

use Module::Runtime qw/use_module/;

binmode STDOUT, ":utf8";

my $logger = Cfm::Common::Logging->setup_logger;
$logger->info('
 ______  ______ __    __
/\  ___\/\  ___/\ "-./  \
\ \ \___\ \  __\ \ \-./\ \
 \ \_____\ \_\  \ \_\ \ \_\
  \/_____/\/_/   \/_/  \/_/  (c) Constantin Weisser, 2017

'
);
$logger->info("Starting CLI ...");

bean 'formatter' => sub {
    use Cfm::Ui::Format::Pretty;
    use Cfm::Ui::Format::Csv;
    use Cfm::Ui::Format::Json;
    my %formatters = (
        pretty => sub {Cfm::Ui::Format::Pretty->instance},
        csv    => sub {Cfm::Ui::Format::Csv->instance},
        json   => sub {Cfm::Ui::Format::Json->instance},
    );

    my $format = Cfm::Config->instance()->require_option("format");
    $logger->debug("Choose format: $format");
    ($formatters{$format} // die "Unknown format: $format")->();
};

bean 'db' => sub {
    use_module('Cfm::Db::Init');
    use_module('DBI');
    use_module('File::Path');

    my $config = Cfm::Config->instance();
    my $home = $config->require_option('home');

    File::Path::make_path($home);
    my $dbfile = $home . '/db.sqlite';
    $logger->info("Open local DB at $dbfile ...");
    my $db = DBI->connect("dbi:SQLite:dbname=$dbfile","","");
    Cfm::Db::Init->setup($db);

    $db;
};

Cfm::Ui::Cli->new()->run(@ARGV);
$logger->info("Application is terminating normally.");
exit 0;
